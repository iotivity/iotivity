Import('env')

arduino_simplecs_env = env.Clone()
######################################################################
# Build flags
######################################################################
arduino_simplecs_env.PrependUnique(CPPPATH = [
		'../../../../../ocsocket/include',
		'../../../../../logger/include',
		'../../../../../stack/include',
		'../../../../../../oc_logger/include'
		])

arduino_simplecs_env.AppendUnique(LIBPATH = [env.get('BUILD_DIR')])
arduino_simplecs_env.PrependUnique(LIBS = ['octbstack', 'coap'])
arduino_simplecs_env.PrependUnique(LINKFLAGS = ['-Wl,-cref', '-Wl,-Map='+Dir('.').path+'/SimpleClientServer.map'])

if not env.get('RELEASE'):
	arduino_simplecs_env.AppendUnique(CPPDEFINES = ['TB_LOG'])

if env.get('NET') == 'Wifi':
	arduino_simplecs_env.AppendUnique(CPPDEFINES = ['ARDUINOWIFI'])

arduino_simplecs = arduino_simplecs_env.Program('SimpleClientServer', 'ocserver.cpp')
env.CreateBin('SimpleClientServer')

#The map file is intermediate file, make sure it's removed when clean build
arduino_simplecs_env.Clean(arduino_simplecs, 'SimpleClientServer.map')

Alias('arduino_simplecs', arduino_simplecs)
env.AppendTarget('arduino_simplecs')

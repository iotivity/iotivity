#******************************************************************
#
# Copyright 2015 Samsung Electronics All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

##
# basis build script
##
import platform

Import('env')

SConscript('src/common/SConscript')
SConscript('src/resourceBroker/SConscript')
SConscript('src/resourceCache/SConscript')
SConscript('src/serverBuilder/SConscript')
SConscript('src/resourceContainer/SConscript')
SConscript('sdk/SConscript')

######################################################################
#building Resource client
######################################################################

if env.get('RELEASE'):
        env.AppendUnique(CCFLAGS = ['-Os'])
        env.AppendUnique(CPPDEFINES = ['NDEBUG'])
else:
        env.AppendUnique(CCFLAGS = ['-g'])

if env.get('LOGGING'):
        env.AppendUnique(CPPDEFINES = ['TB_LOG'])

# Add third party libraries
lib_env = env.Clone()
SConscript(env.get('SRC_DIR') + '/service/third_party_libs.scons', 'lib_env')

resourceClient_env = lib_env.Clone()
target_os = env.get('TARGET_OS')
######################################################################
# Build flags
######################################################################
resourceClient_env.AppendUnique(CPPPATH = ['include'])
resourceClient_env.AppendUnique(CPPPATH = ['src/common/primitiveResource/include'])
resourceClient_env.AppendUnique(CPPPATH = ['src/common/expiryTimer/include'])
resourceClient_env.AppendUnique(CPPPATH = ['src/resourceBroker/include'])
resourceClient_env.AppendUnique(CPPPATH = ['src/resourceCache/include'])


resourceClient_env.AppendUnique(LIBPATH = [env.get('BUILD_DIR')])

resourceClient_env.PrependUnique(LIBS = ['oc', 'service_common', 'ResourceBroker' , 'ResourceCache', 'octbstack', 'gnustl_shared','oc_logger', 'compatibility', 'log'])


if target_os not in ['windows', 'winrt']:
    resourceClient_env.AppendUnique(CXXFLAGS = ['-O2', '-g', '-Wall', '-fmessage-length=0', '-std=c++0x'])
if target_os == 'linux':
   resourceClient_env.AppendUnique(LIBS = ['pthread'])

######################################################################
# Source files and Targets
######################################################################
resourceClient_src = 'src/'
client_src = [
        resourceClient_src+ 'ResourceClient.cpp']
ResourceClientsdk = resourceClient_env.StaticLibrary('ResourceClient', client_src)
resourceClient_env.InstallTarget(ResourceClientsdk , 'libResourceClient')

######################################################################
# Build Sample App: SampleResourceClient & SampleResourceServer
######################################################################
SConscript('examples/SConscript')

######################################################################
# Build UnitTests Resource Client
################################################ ######################
SConscript('unittests/SConscript')

